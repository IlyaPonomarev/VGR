
#Область СлужебныеПроцедурыИФункции

// Подсистема расчета бонусов партнеров обработка заполнения.
// 
// Параметры:
//  Объект - ДокументОбъект.ПриобретениеУслугПрочихАктивов - Объект
//  ДанныеЗаполнения - ДокументСсылка.РА_ПодсистемаРасчетаБонусовПартнеров_НачислениеБонусовПартнерам - Данные заполнения
//  СтандартнаяОбработка - Булево - Стандартная обработка
&После("ОбработкаЗаполнения")
Процедура РА_ПодсистемаРасчетаБонусовПартнеровОбработкаЗаполнения(Объект, ДанныеЗаполнения, СтандартнаяОбработка) Экспорт

	Если ТипЗнч(ДанныеЗаполнения) <> Тип(
		"ДокументСсылка.РА_ПодсистемаРасчетаБонусовПартнеров_НачислениеБонусовПартнерам") Тогда
		Возврат;
	КонецЕсли;

	РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "Партнер, Контрагент");

	Объект.Валюта = Константы.ВалютаРегламентированногоУчета.Получить();

	Объект.ВалютаВзаиморасчетов = Объект.Валюта;

	Объект.Партнер = РеквизитыОснования.Партнер;
	Объект.Контрагент = РеквизитыОснования.Контрагент;
		
	//Добавим значение в табличную часть документа 
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СУММА(ЗапросНачисления.СуммаБонуса) КАК СуммаБонуса,
	|	СУММА(ЗапросНачисления.СуммаНДС) КАК СуммаНДС,
	|	ЗапросНачисления.СтавкаНДС КАК СтавкаНДС
	|ИЗ
	|	(ВЫБРАТЬ
	|		РА_НачислениеБонусовПартнерамНачисленияАвтоматическийРасчет.Сумма КАК СуммаБонуса,
	|		РА_НачислениеБонусовПартнерамНачисленияАвтоматическийРасчет.СуммаНДС КАК СуммаНДС,
	|		РА_НачислениеБонусовПартнерамНачисленияАвтоматическийРасчет.СтавкаНДС КАК СтавкаНДС
	|	ИЗ
	|		Документ.РА_ПодсистемаРасчетаБонусовПартнеров_НачислениеБонусовПартнерам.НачисленияАвтоматическийРасчет КАК РА_НачислениеБонусовПартнерамНачисленияАвтоматическийРасчет
	|	ГДЕ
	|		РА_НачислениеБонусовПартнерамНачисленияАвтоматическийРасчет.Ссылка = &Основание
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РА_НачислениеБонусовПартнерамНачисленияАвтоматическийРасчет.Сумма,
	|		РА_НачислениеБонусовПартнерамНачисленияАвтоматическийРасчет.СуммаНДС,
	|		РА_НачислениеБонусовПартнерамНачисленияАвтоматическийРасчет.СтавкаНДС
	|	ИЗ
	|		Документ.РА_ПодсистемаРасчетаБонусовПартнеров_НачислениеБонусовПартнерам.НачисленияРучнойРасчет КАК РА_НачислениеБонусовПартнерамНачисленияАвтоматическийРасчет
	|	ГДЕ
	|		РА_НачислениеБонусовПартнерамНачисленияАвтоматическийРасчет.Ссылка = &Основание) КАК ЗапросНачисления
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗапросНачисления.СтавкаНДС
	|";

	Запрос.УстановитьПараметр("Основание", ДанныеЗаполнения);

	ВыборкаСтрок = Запрос.Выполнить().Выбрать();

	Пока ВыборкаСтрок.Следующий() Цикл

		СтрокаРасходы = Объект.Расходы.Добавить();
		СтрокаРасходы.Количество = 1;
		СтрокаРасходы.СтавкаНДС = ВыборкаСтрок.СтавкаНДС;
		СтрокаРасходы.Содержание = "";
		//@skip-check wrong-type-expression
		СтрокаРасходы.Сумма = ВыборкаСтрок.СуммаБонуса;
		//@skip-check wrong-type-expression
		СтрокаРасходы.СуммаСНДС = ВыборкаСтрок.СуммаБонуса + ВыборкаСтрок.СуммаНДС;

		СтрокаРасходы.СуммаНДС = ВыборкаСтрок.СуммаНДС;

		СтрокаРасходы.Цена = ВыборкаСтрок.СуммаБонуса;

	КонецЦикла;

КонецПроцедуры

#КонецОбласти
