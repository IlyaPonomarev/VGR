#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипПериода = ПредопределенноеЗначение("Перечисление.РА_ТипыПериодов.Квартал") Тогда
		
		НачалоПериода = НачалоКвартала(НачалоПериода);
		КонецПериода = КонецКвартала(КонецПериода);
		
		Если НачалоКвартала(НачалоПериода) <> НачалоКвартала(КонецПериода) Тогда
			ОбщегоНазначения.СообщитьПользователю("Начало и конец периода должны быть в одном квартале!",,,,Отказ);	
		КонецЕсли;
		
	ИначеЕсли ТипПериода = ПредопределенноеЗначение("Перечисление.РА_ТипыПериодов.Полугодие") Тогда 
		
		НачалоПериода = НачалоМесяца(НачалоПериода);
		КонецПериода = КонецМесяца(КонецПериода);
		
		Если (Месяц(КонецПериода) - Месяц(НачалоПериода))+1 <> 6 Тогда
			ОбщегоНазначения.СообщитьПользователю("Начало и конец периода должны быть в одном полугодии!",,,,Отказ);	
		КонецЕсли;
		
	ИначеЕсли ТипПериода = ПредопределенноеЗначение("Перечисление.РА_ТипыПериодов.Месяц") Тогда 
		
		НачалоПериода = НачалоМесяца(НачалоПериода);
		КонецПериода = КонецМесяца(КонецПериода);
		
		Если НачалоМесяца(НачалоПериода) <> НачалоМесяца(КонецПериода) Тогда
			ОбщегоНазначения.СообщитьПользователю("Начало и конец периода должны быть в одном месяце!",,,,Отказ);	
		КонецЕсли;	
		
	ИначеЕсли ТипПериода = ПредопределенноеЗначение("Перечисление.РА_ТипыПериодов.Год") Тогда 
		
		НачалоПериода = НачалоГода(НачалоПериода);
		КонецПериода = КонецГода(КонецПериода);
		
		Если НачалоГода(НачалоПериода) <> НачалоГода(КонецПериода) Тогда
			ОбщегоНазначения.СообщитьПользователю("Начало и конец периода должны быть в одном году!",,,,Отказ);	
		КонецЕсли;	
		
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	РА_ПериодыДействияБонуса.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.РА_ПодсистемаРасчетаБонусовПартнеров_ПериодыДействияБонуса КАК РА_ПериодыДействияБонуса
		|ГДЕ
		|	РА_ПериодыДействияБонуса.НачалоПериода = &НачалоПериода
		|	И РА_ПериодыДействияБонуса.КонецПериода = &КонецПериода
		|	И РА_ПериодыДействияБонуса.ТипПериода = &ТипПериода
		|	И РА_ПериодыДействияБонуса.Ссылка <> &Ссылка";
		
		Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
		Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("ТипПериода", ТипПериода);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если Не РезультатЗапроса.Пустой() Тогда
			ОбщегоНазначения.СообщитьПользователю("Запись с такими ключевыми полями уже существует!",,,,Отказ);		
		КонецЕсли;
		
		Если ТипПериода = ПредопределенноеЗначение("Перечисление.РА_ТипыПериодов.Квартал") Тогда
			Наименование = Формат(НачалоПериода,"ЧГ=; ДФ='кк ""квартал"" гггг""г.""'");
		ИначеЕсли ТипПериода = ПредопределенноеЗначение("Перечисление.РА_ТипыПериодов.Год") Тогда
			Наименование = Формат(НачалоПериода,"ДФ='гггг""г.""'");
		ИначеЕсли ТипПериода = ПредопределенноеЗначение("Перечисление.РА_ТипыПериодов.Месяц") Тогда
			Наименование = Формат(НачалоПериода,"ДФ='ММММ гггг""г.""'");
		ИначеЕсли ТипПериода = ПредопределенноеЗначение("Перечисление.РА_ТипыПериодов.Полугодие") Тогда
			Наименование = СтрШаблон("%1 полугодие %2",?(Месяц(НачалоПериода)<7,"01","02"),Формат(НачалоПериода,"ДФ='гггг""г.""'"));	
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли