#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
			
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РА_ПодсистемаРасчетаБонусовПартнеров_БонусныеПрограммы.Ссылка
	|ИЗ
	|	Справочник.РА_ПодсистемаРасчетаБонусовПартнеров_БонусныеПрограммы КАК
	|		РА_ПодсистемаРасчетаБонусовПартнеров_БонусныеПрограммы
	|ГДЕ
	|	РА_ПодсистемаРасчетаБонусовПартнеров_БонусныеПрограммы.Владелец = &Владелец
	|	И РА_ПодсистемаРасчетаБонусовПартнеров_БонусныеПрограммы.Валюта = &Валюта
	|	И РА_ПодсистемаРасчетаБонусовПартнеров_БонусныеПрограммы.ПериодДействия = &ПериодДействия
	|	И РА_ПодсистемаРасчетаБонусовПартнеров_БонусныеПрограммы.ТипПоказателяПлана = &ТипПоказателяПлана
	|	И РА_ПодсистемаРасчетаБонусовПартнеров_БонусныеПрограммы.Ссылка <> &Ссылка
	|	И НЕ РА_ПодсистемаРасчетаБонусовПартнеров_БонусныеПрограммы.ПометкаУдаления";

	Запрос.УстановитьПараметр("ПериодДействия", ПериодДействия);
	Запрос.УстановитьПараметр("Валюта", Валюта);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ТипПоказателяПлана", ТипПоказателяПлана);
	Запрос.УстановитьПараметр("Владелец", Владелец);

	РезультатЗапроса = Запрос.Выполнить();

	Если Не РезультатЗапроса.Пустой() Тогда

		ОбщегоНазначения.СообщитьПользователю(
			"Бонусная программа с такими настройками уже существует! Запись невозможна!", , , , Отказ);

	КонецЕсли;
	
	Если ТипЗнч(Владелец) = Тип("СправочникСсылка.Партнеры") Тогда

		Партнер = Владелец;

	ИначеЕсли ТипЗнч(Владелец) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда

		Партнер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Владелец, "Партнер");

	КонецЕсли;
	
	Если ЭтоНовый() И Не ЗначениеЗаполнено(Наименование) Тогда

		Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1, %2",
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Владелец, "Наименование"),
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПериодДействия, "Наименование"));

	КонецЕсли;	

КонецПроцедуры

#КонецОбласти

#КонецЕсли