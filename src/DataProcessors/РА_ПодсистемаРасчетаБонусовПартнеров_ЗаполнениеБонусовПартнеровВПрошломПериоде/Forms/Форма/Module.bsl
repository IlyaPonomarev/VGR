
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НачислитьБонусы(Команда)
	
	Если Не ЗначениеЗаполнено(Период.ДатаНачала) ИЛИ Не ЗначениеЗаполнено(Период.ДатаОкончания) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Некорректно заполнен период!");
		Возврат;
	КонецЕсли;
	
	ЕстьОтборы = Ложь;
	
	Если ЗначениеЗаполнено(Партнер) Тогда
		ЕстьОтборы = Истина;
	ИначеЕсли ЗначениеЗаполнено(Договор) Тогда
		ЕстьОтборы = Истина;
	ИначеЕсли ЗначениеЗаполнено(Документ) Тогда
		ЕстьОтборы = Истина;
	КонецЕсли;
	
	Если НЕ ЕстьОтборы Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не заполнены отборы!");
		Возврат;		
	КонецЕсли;	
	
	НачислитьБонусыНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НачислитьБонусыНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВложенныйЗапрос.Ссылка КАК ДокументСсылка
		|ИЗ
		|	(ВЫБРАТЬ
		|		РеализацияТоваровУслуг.Ссылка КАК Ссылка,
		|		РеализацияТоваровУслуг.Дата КАК Дата
		|	ИЗ
		|		Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|	ГДЕ
		|		ВЫБОР
		|				КОГДА &ОтборПоДоговору
		|					ТОГДА РеализацияТоваровУслуг.Договор = &Договор
		|				ИНАЧЕ ИСТИНА
		|			КОНЕЦ
		|		И РеализацияТоваровУслуг.Проведен
		|		И РеализацияТоваровУслуг.Дата >= &ДатаНачала
		|		И РеализацияТоваровУслуг.Дата <= &ДатаОкончания
		|		И ВЫБОР
		|				КОГДА &ОтборПоДокументу
		|					ТОГДА РеализацияТоваровУслуг.Ссылка = &Документ
		|				ИНАЧЕ ИСТИНА
		|			КОНЕЦ
		|		И ВЫБОР
		|				КОГДА &ОтборПоПартнеру
		|					ТОГДА РеализацияТоваровУслуг.Партнер = &Партнер
		|				ИНАЧЕ ИСТИНА
		|			КОНЕЦ
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ВозвратТоваровОтКлиента.Ссылка,
		|		ВозвратТоваровОтКлиента.Дата
		|	ИЗ
		|		Документ.ВозвратТоваровОтКлиента КАК ВозвратТоваровОтКлиента
		|	ГДЕ
		|		ВозвратТоваровОтКлиента.Проведен
		|		И ВЫБОР
		|				КОГДА &ОтборПоДоговору
		|					ТОГДА ВозвратТоваровОтКлиента.Договор = &Договор
		|				ИНАЧЕ ИСТИНА
		|			КОНЕЦ
		|		И ВозвратТоваровОтКлиента.Дата >= &ДатаНачала
		|		И ВозвратТоваровОтКлиента.Дата <= &ДатаОкончания
		|		И ВЫБОР
		|				КОГДА &ОтборПоДокументу
		|					ТОГДА ВозвратТоваровОтКлиента.Ссылка = &Документ
		|				ИНАЧЕ ИСТИНА
		|			КОНЕЦ
		|		И ВЫБОР
		|				КОГДА &ОтборПоПартнеру
		|					ТОГДА ВозвратТоваровОтКлиента.Партнер = &Партнер
		|				ИНАЧЕ ИСТИНА
		|			КОНЕЦ
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		КорректировкаРеализации.Ссылка,
		|		КорректировкаРеализации.Дата
		|	ИЗ
		|		Документ.КорректировкаРеализации КАК КорректировкаРеализации
		|	ГДЕ
		|		КорректировкаРеализации.Проведен
		|		И ВЫБОР
		|				КОГДА &ОтборПоДоговору
		|					ТОГДА КорректировкаРеализации.Договор = &Договор
		|				ИНАЧЕ ИСТИНА
		|			КОНЕЦ
		|		И КорректировкаРеализации.Дата >= &ДатаНачала
		|		И КорректировкаРеализации.Дата <= &ДатаОкончания
		|		И ВЫБОР
		|				КОГДА &ОтборПоДокументу
		|					ТОГДА КорректировкаРеализации.Ссылка = &Документ
		|				ИНАЧЕ ИСТИНА
		|			КОНЕЦ
		|		И ВЫБОР
		|				КОГДА &ОтборПоПартнеру
		|					ТОГДА КорректировкаРеализации.Партнер = &Партнер
		|				ИНАЧЕ ИСТИНА
		|			КОНЕЦ) КАК ВложенныйЗапрос
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВложенныйЗапрос.Дата";
	
	Запрос.УстановитьПараметр("Договор", Договор);
	Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания);
	Запрос.УстановитьПараметр("ОтборПоДоговору", ЗначениеЗаполнено(Договор));
	Запрос.УстановитьПараметр("ОтборПоДокументу",?(Документ <> Неопределено И Не Документ.Пустая(), Истина, Ложь));
	Запрос.УстановитьПараметр("Документ", Документ);
	Запрос.УстановитьПараметр("ОтборПоПартнеру", ЗначениеЗаполнено(Партнер));
	Запрос.УстановитьПараметр("Партнер", Партнер);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ДокументОбъект = ВыборкаДетальныеЗаписи.ДокументСсылка.ПолучитьОбъект();
		РА_ПодсистемаРасчетаБонусовПартнеров_РасчетБонусовСервер.ОбработатьНачислениеБонусов(ДокументОбъект, Истина);
	КонецЦикла;

КонецПроцедуры

#КонецОбласти